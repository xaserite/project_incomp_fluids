real L=0.5, H=0.25;
int entry = 99, lim = 98, exit = 97;
int Nh = 10;
real nu = 8.9e-4;
real u0 = 10.;
real omega = 1.;
real t = 0.;
real Tmax = 4*pi;
int Nit = 100;
real dt = Tmax/Nit;
// domain and mesh building
border a(t=L,1){x=t;y=0; label=lim;}
border b(t=0,1){x=1;y=t; label=exit;}
border c(t=0,1){x=1-t;y=1; label=lim;}
border d(t=0,1-H){x=0;y=1-t; label=entry;}
border e(t=0,L){x=t;y=H; label=lim;}
border f(t=0,H){x=L;y=H-t; label=lim;}

plot(a(10),b(10),c(10),d(10),e(10),f(10),wait=1);
mesh Th = buildmesh(a(Nh) + b(Nh) + c(Nh) + d(Nh) + e(Nh) + f(Nh));
plot(Th,wait=1);
// FE spaces
fespace Uh(Th,P2); Uh u=0,v=0,uu,vv;
fespace Ph(Th,P1); Ph p,pp;
// compute
for(int i=0;i<Nit;i++)
{
	Uh uold = u, vold = v;
  solve stokes([u,v,p],[uu,vv,pp]) =
    int2d(Th)(u*uu + v*vv + dt*nu*(dx(u)*dx(uu)+dy(u)*dy(uu) + dx(v)*dx(vv)+ dy(v)*dy(vv))
			+ dt*dx(p)*uu + dt*dy(p)*vv + dt*pp*(dx(u)+dy(v))
			- dt*1e-10*p*pp)
			- int2d(Th)(uold*uu + vold*vv)
		+ on(lim,u=0,v=0) + on(entry,u=u0*sin(omega*t)^2,v=0) 
		+ on(exit,u=(1-H)*u0*sin(omega*t)^2,v=0);
  t += dt;
  plot([u,v],p,wait=0,value=1,cmm="time = "+t);
} 